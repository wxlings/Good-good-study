**因：**

 上周周会在分享Dart构造方法相关知识时，组内小伙伴提出了几个构造函数的问题，当时有些懵，课后整理一波.

1. 构造方法有返回值么？

2. 构造方法返回枚举实例问题

   

**分享原文给出的关于构造函数几个概念：**

 构造函数 ，是一种特殊的方法。**主要用来在创建对象时初始化对象， 即为对象成员变量赋初始值**，总与new运算符一起使用在创建对象的语句中。**特别的一个类可以有多个构造函数 ，可根据其参数个数的不同或参数类型的不同来区分它们** 即构造函数的***重载***。 

##### **构造函数的特点：**

1. **构造函数的命名必须和类名完全相同**。在Java中普通函数可以和构造函数同名，但是必须带有返回值；
2. **构造函数的功能主要用于在类的对象创建时定义初始化的状态。它没有返回值，也不能用void来修饰。**这就保证了它不仅什么也不用自动返回，而且根本不能有任何选择。而其他方法都有返回值，即使是void返回值。尽管方法体本身不会自动返回什么，但仍然可以让它返回一些东西，而这些东西可能是不安全的；
3. **构造函数不能被直接调用**，必须通过new运算符在创建对象时才会自动调用；而一般的方法是在程序执行到它的时候被调用的；
4. 当定义一个类的时候，通常情况下都会显示该类的构造函数，并在函数中指定初始化的工作也可省略，不过**Java,Dart编译器会提供一个默认的构造函数.此默认构造函数是不带参数的。**而一般的方法不存在这一特点；
5. **构造函数有回滚的效果**，构造函数抛出异常时，构造的是一个不完整对象，会回滚，将此不完整对象的成员释放(c++) 
6. **当一个类只定义了私有的构造函数，将无法通过new关键字来创建其对象**，当一个类没有定义任何构造函数，C#编译器会为其自动生成一个默认的无参的构造函数。(私有构造函数，例如创建单例)
7. 在Python中构造函数必须通过重写`__init__`方法实现

关于第二点`构造方法没有返回值`就提出了疑问，我们在`new`一个对象的时候会调用构造器，虽然我们并没有使用`return this`的写法，但在不同的语言里经过编译后会有不同实现。

嗯，这里勾起了我的疑惑：这里这么多年的`new`对象，**到底是先创建实例再调用构造器初始化实例呢 ？还是先执行这个特殊的函数给我们返回一个实例呢 ？**



#### 构造方法与返回值

我们先看一个栗子：

```java

class Person{
    
    private String name;
    
    Person(String name){
        this.name = name;
    }
}

Person person = new Person("张三");

```

我们有一个Person类，在构造时方法中需要传入一个参数name，然后在方法内通过`this`也就当前对象的实例引用来赋值给实例属性name.

这样，我们就可以猜想是先创建Person的实例，然后构造器来初始化实例的属性，没错一定是这样。

如果按照以上代码是这样的，可是我们前面也说了**构造方法是特殊的方法**，会不会编译后编译器给我们加上了类似`return this`的指令呢 ？怎么办呢？ 只能看字节码指令

```java
// compiled from: Person.java
public class com/example/myapplication/Person {
  
  private Ljava/lang/String; name
  // 构造方法：需要传入String参数
  <init>(Ljava/lang/String;)V
   L0
    LINENUMBER 7 L0
    ALOAD 0 								// load局部变量表index=0的数据到栈顶
    INVOKESPECIAL java/lang/Object.<init> ()V  // 调用父类工造方法
   L1
    LINENUMBER 8 L1
    ALOAD 0
    ALOAD 1									// load局部变量表index=1的数据
    PUTFIELD com/example/myapplication/Person.name : Ljava/lang/String; //复制变量name属性
   L2
    LINENUMBER 9 L2
    RETURN									// 构造方法执行完，无返回结果
   L3
    LOCALVARIABLE this Lcom/example/myapplication/Person; L0 L3 0 // 局部表 this
    LOCALVARIABLE name Ljava/lang/String; L0 L3 1				// 局部变量表 name
    MAXSTACK = 2
    MAXLOCALS = 2

  // access flags 0x9
  public static main([Ljava/lang/String;)V
   L0
    LINENUMBER 12 L0
    NEW com/example/myapplication/Person // 创建Person实例并压入栈内
    DUP								  // 复制栈顶数据再次压入栈顶
    LDC "\u5f20\u4e09"				    // 把常量引用数据`张三`压入栈内
    INVOKESPECIAL com/example/myapplication/Person.<init> (Ljava/lang/String;)V // 调用Peron构造方法
    ASTORE 1  						   // 把栈顶引用数据保存到局部变量表index=1位置
   L1
    LINENUMBER 13 L1
    RETURN								// main() 方法执行结束
   L2
    LOCALVARIABLE args [Ljava/lang/String; L0 L2 0
    LOCALVARIABLE person Lcom/example/myapplication/Person; L1 L2 1
    MAXSTACK = 3
    MAXLOCALS = 2
}

```



简要分析，由入口点`main()`开始：

1. `NEW com/example/myapplication/Person` 创建Person实例，并压入栈内
2.  `DUP` 复制栈顶数据
3.  `LDC` 把常量”张三“压入栈
4. `INVOKESPECIAL .../Person.<init> (Ljava/lang/String;)V` 调用类的构造方法并传入栈顶数据
5.  构造方法内就是通过`PUTFIELD`保存name属性，然后正常`return`
6.  把实例保存到局部变量表

至此，`Person person = new Person()`的过程我们已经简单分析得出，对于`jvm`而言，创建对象的过程，是先分配内存，创建实例，调用构造方法...的流程，当然还有涉及了一些类加载的知识，这里不多说。也查找了`JS`中`new`的一些知识，下面给出一些伪代码：

```js

   var ObjectFactory = function () {
       // 创建一个对象
       var obj = new Object()
       // 返回第一个参数作为构造函数
       var Constructor = [].shift.call(arguments)
       // 将构造函数的原型复制于对象的原型
       obj.__proto__ = Constructor.prototype
       // 调用构造函数，并将obj 作为this, arguments作为参数
       var ret = Constructor.apply(obj, arguments)
       // 如果构造函数里返回一个对象的话，就直接返回，否则我们就返回this即new创建的对象
       return typeof ret === 'object'? ret: obj
   }
   
```

大致的流程：

1. 创建一个实例：
2. 返回第一参数作为构造方法
3. 把构造方法赋值给原型对象
4. 返回该实例引用

这样看来再`JS`中`new`一个对象的实例更加符合猜测的第二种类型。

**小结：不用的语言实现却有不同**

1. 对于`Java`,`kotin`由字节码角度看构造方法的作用更接近初始化属性。
2. 猜测：对于`Js`等弱类型语言构造方法更有些偏向于返回实例的意思。



#### 构造方法与枚举

对于大多数编程语言都有枚举`enum`的概念，这里对`Java/Kotlin/Dart`的枚举学习一波；

#### Java中枚举的定义与使用：

```java

enum Color {
    Red("红色"), Green("绿色"), Blue("蓝色");

    private final String cnName;

    Color(String cnName) {
        this.cnName = cnName;
    }

    String getCnName() {
        return cnName;
    }
}

// 测试类
class Test {
    public static void main(String[] args) {
        Color red = Color.Red;
        System.out.println(red.getCnName());
     
        // 创建white,编译报错：Enum types cannot be instantiated
        // Color white = new Color("WHITE");
    }
}

```

> 注意：在Java中我们不能通过`new Color()`的方式创建`Color`的新实例。不然编译报错：
>
> ![error]( https://qncdn.wanshifu.com/da64e6a440f85df830a5e14f7358c83c )



定义一个枚举类的写法很简单，它遵循了类的语义，却与类的实例方式不同，为什么呢？我们根据字节指令来看下：

```java
final enum com/example/application/Color extends java/lang/Enum { }
```

哦，第一行就标识了枚举类`Color`是继承`Enum`的，好的我们简单看一下`Enum`是什么？

```java

/**
This is the common base class of all Java language enumeration types.
关于枚举的更多信息，包括编译器合成的隐式声明方法的描述，可以在8.9节中找到  	 
*/
public abstract class Enum<E extends Enum<E>> implements Comparable<E>, Serializable {
    
    // The name of this enum constant, as declared in the enum declaration.
    private final String name;
    
    protected Enum(String name, int ordinal) {
        this.name = name;
        this.ordinal = ordinal;
    }
    
    // 类的静态方法，根据枚举类型和名称查找枚举值
    public static <T extends Enum<T>> T valueOf(Class<T> enumType, String name) {
        // ... 空判断/空处理
        // Iterate backwards through the array to retain historic Android behavior in the
        // unexpected / likely invalid case where there are multiple values with the same name.
        for (int i = values.length - 1; i >= 0; --i) {
            T value = values[i];
            if (name.equals(value.name())) {
                return value;
            }
        }
    }
    
    /// 静态方法：根据枚举类型获取对应的枚举数组（内部通过反射）
    public static <T extends Enum<T>> T[] getSharedConstants(Class<T> enumType) {
        return (T[]) sharedConstantsCache.get(enumType);
    }
}

```

这里`Enum`是一个抽象类，那么可以假定`enum`是特殊的`class`类型。同时它实现了`Serializable`接口。

```java

public final enum com/example/application/Color extends java/lang/Enum {

    // 声明3个静态final enum变量Red,Green and Blue;
    public final static enum Lcom/example/application/Color; Red
    public final static enum Lcom/example/application/Color; Green
    public final static enum Lcom/example/application/Color; Blue
     
  	// 属性：name
  	public Ljava/lang/String; cnName

  	// ... 省略 values() ， valueOf(String) 及 getCnName()实现
                                    
  	// declaration: void <init>(java.lang.String) 声明私有构造方法，需要传入2个String参数
  	private <init>(Ljava/lang/String;ILjava/lang/String;)V
   		L0
    		LINENUMBER 9 L0
    		ALOAD 0 
    		ALOAD 1
    		ILOAD 2
    		INVOKESPECIAL java/lang/Enum.<init> (Ljava/lang/String;I)V // 调用Enum构造函数指定参数
   		L1
    		LINENUMBER 10 L1
    		ALOAD 0
    		ALOAD 3
    		PUTFIELD com/example/application/Color.cnName : Ljava/lang/String; // 设置对象变量cnName
   		L2
    		LINENUMBER 11 L2
    		RETURN
   		L3
    		LOCALVARIABLE this Lcom/example/application/Color; L0 L3 0
    		LOCALVARIABLE cnName Ljava/lang/String; L0 L3 3
    		MAXSTACK = 3
    		MAXLOCALS = 4
        
    // 静态代码块,创建枚举值实例及枚举数组，在类加载时执行
  	static <clinit>()V
      L0
        LINENUMBER 12 L0
        NEW com/example/application/Color  // 创建Color实例，并压入栈
        DUP								// 复制Color实例，并再次压入栈顶(此时栈顶为俩个连续Color实例)
        LDC "Red"					     // 把常量池中的Red压入栈
        ICONST_0						 // 把数值常量压入栈（这里没有理解）
        LDC "\u7ea2\u8272"				  // 把常量池中的`红色`压入栈
        INVOKESPECIAL com/example/application/Color.<init> (Ljava/lang/String;ILjava/lang/String;)V
                						 // 调用栈顶Color实例的构造函数初始化参数
        PUTSTATIC com/example/application/Color.Red : Lcom/example/application/Color;
    									// 设置static变量Red为当前栈顶实例引用
        ...Green及Blue同于Red的初始化
       L1
        LINENUMBER 11 L1
        ICONST_3
        ANEWARRAY com/example/application/Color // 创建引用类型<Color>数组
        DUP									 // 复制数据对象再压入栈
        ICONST_0
        GETSTATIC com/example/application/Color.Red : Lcom/example/application/Color; // 获取枚举值Red
        AASTORE                                 // 保存引用类型值到局部变量表并出栈
        DUP
         ...Green及Blue同于Red的加入数组
        PUTSTATIC com/example/application/Color.$VALUES : [Lcom/example/application/Color;
        RETURN								 // 把数组赋值给$value
        MAXSTACK = 5
        MAXLOCALS = 0
}
                                                 
```

**通多字节码我们可以得出：**

1. 我们定义的枚举类`Color.java`,经过编译器编译后会被继承抽象类`Enum`，是特殊的类类型。

2. 编译时会在`enum`类内部创建所有声明的`枚举值变量`并且标记为静态且不可以变。

3. 编译时同时会声明静态代码块，用于初始创建各个枚举值的实例并赋值。

4. 根据类加载的特性，静态代码块会在类加载时调用，也就是说枚举实例会在类加载时被创建。先创建对象实例，然后调用类的构造器初始变量及属性，最后把具体实例引用通过`PUTSTATIC`赋值给各个静态变量。

5. 正是因为正常情况下类仅会被加载一次，静态代码块仅会被执行一次，所有有时用过枚举来时实现单例也是可以的。

   

下面是`main()`的具体实现：

```java

 @groovyx.ast.bytecode.Bytecode
  public static void main(String[] a) {
     // 获取静态变量 Color.REd
    getstatic 'com/example/myapplication/Color.Red','Lcom/example/myapplication/Color;'
    // 保存引用类型数据到局部变量表index=1，并弹出栈
    astore 1
    // 获取静态变量 System.out
    getstatic 'java/lang/System.out','Ljava/io/PrintStream;'
    // 加载引用数据类型-局部变量表index=1(即)数据到栈顶
    aload 1
    // 调用方法：getCnName()把结果值压入栈     
    invokevirtual 'com/example/myapplication/Color.getCnName','()Ljava/lang/String;'
    // 调用方法：println() 打印log
    invokevirtual 'java/io/PrintStream.println','(Ljava/lang/String;)V'
    return
  }

```



#### `Kotlin`中的枚举：

我们已同样的方式进行`enum`声明和使用：

```kotlin

fun main() {
    val red = Color.Red
    println(red.cnName)
}

internal enum class Color(val cnName: String) {
    Red("红色"), Green("绿色"), Blue("蓝色");
}

```

经过编译后，虽然`kotlin`的`TestKt.class`文件与Java的`Test.class`有所不同，但映射到`JVM`字节码指令是一样的，因为他们都是基于`JVM`环境，这里就不贴字节源码了。

```kotlin

internal final enum class Color private constructor(cnName: kotlin.String) : 
kotlin.Enum<com.example.application.Color> {
    Red, Green, Blue;
    public final val cnName: kotlin.String /* compiled code */
}

```

> 如果使用`Android Studio`那么Java文件的`class`文件在`build/intermediates/javac/**/classes/package/`
>
> 而`kotlin`文件对应的`class`文件在`temp/kotlin-classes/pagename/`

其实在`kolin`中还有一个密封类的概念，支持我们自定其实例，对于枚举值扩展逻辑更实用。[官链传送](https://www.kotlincn.net/docs/reference/sealed-classes.html#%E5%AF%86%E5%B0%81%E7%B1%BB)



#### 最后我们再看下Dart中的枚举：

Dart version 在2.17以上才支持增强枚举类；

```dart

void main() {
  var color = Color.red;
  print(color);
}

enum Color {
  red("红色"),
  green("绿色");

  final String cnName;

  const Color(this.cnName);
}

```

因为Dart是基于`DVM(Dart VM运行时)`，暂时没有找到类似查看字节码的工具进行反编

但是按照`Java enum`的逻辑，Dart中确实存在抽象类`Enum`。

```dart

@Since("2.14")
abstract class Enum {
    ...
}

/// Superclass of all enum class implementations.
abstract class _Enum implements Enum {
  final int index;
  final String _name;
  const _Enum(this.index, this._name);
}

```

找了一下官网并没有给出可以解析编译后的`.dill`文件命令，也查找了一些博客大部分都是通过`dump_kernel`命令进行解析，但基本都是历史版本，再新版本中并未找到。

所以这里我们先猜想一下`_Enum`，文档给出的说明该抽象类是所有`enum`类的父类。那么按照Jvm指令规范，它也应该是特殊的类类型。

这里先做个TAG,后面再想办法根据具体的指令实现来确认`enum`的本体。



**得：**

1. 声明的枚举值是枚举类型的具体实现，并且枚举值名称唯一。
2. 枚举值是编译期产物，在编译器确认其具体类型，初始化。
3. 宽泛假定：枚举是一种特殊的类，他们最终会继承抽象类`Enum`，而遵循类的语义。
4. 因为`enum`类比较特殊，基本没有办法学习太多，通过字节码指令可以更清晰的了解其类型知识。顺便复习了一下字节码及内存模型等相关知识。
5.  枚举类是封闭的，即不能被继承、被实现、被混入或显式被实例化。 



> 字节码指令知识扩展
>
>  **A 字节码和数据类型：**
>
> - 大多数指令都包含了操作所对应的数据类型信息，比如`iload`从局部变量表中加载int数据到操作数栈
> - 各个类型的数据简写：`i` 代表`int`,`l` 代表`long`,`s`代表`short`,`b`代表`byte`,`c`代表`char`,`f`代表`float`,`	d`代表`double`.
> - 同时有个指令看不出具体的数据类型，如`newarray`.
> - `byte`,`short`,`char`,`boolean`没有指令支持，在编译运行时会将其拓展为`int`类型
>
> **B 指令分类：**
>
> - 大致分为9类：加载与存储；算数；类型转换；对象的创建与访问；方法的调用与返回、操作数栈管理、比较控制、异常处理、同步控制等
> - 指令可以从局部变量表、常量池、堆中对象、方法调用、系统调用中取得数据（值或引用都可以），然后压入操作数栈
> - 可以从操作数栈中取出一个或多个值，完成加减乘除或其他操作
>
> **C 加载与存储：**
>
> - 将数据在栈帧的局部变量表和操作数栈中来回传递
> - 有的指令，虽然没有操作数，但是会将操作数隐含在操作码中，比如`iload_0`局部变量表中索引为0的数据压入操作数栈中
>
> c-1 局部变量压栈指令
>
> - 将局部变量表中的数据压入栈中
> - 指令大体分为两类：
> - 1. `xload_<n>`，x可以是i、l、f、d、a，n是0到3，代表局部变量表索引
>   2. `xload`，x可以是i、l、f、d、a
>
> c-2 常量入栈指令
>
> - 将常数压入操作数栈，根据数据类型和入栈内容不同，分为`const`、`push`、`ldc`系列
> - `const`系列指令：用于特定的常量（具体的数字）入栈，操作数包含在操作码中
> - 1.  `iconst_<i>`：i的范围是-1到5，表示一个实际的数  ，如`iconst_m1`压入-1，`iconst_4`压入4 
>   2. `lconst_<l>`：l的范围是0到1
>   3. `fconst_<f>`：f的范围是0到2
>   4. `dconst_<d>`：d的范围是0到1
>   5. `aconst_null`：引用类型，默认为null，压入null
> - `push`指令系列
> - 1. `bipush`：接收8位整数入栈（-128~127）
>   2. `sipush`：接收16位整数入栈（-32768~32767）
> - `ldc`指令系列：万能指令，以上两种都不行的，由`idc`实现
> - 1. `ldc`接收8位参数，指向常量池中的int、float或String，将常量池中指定内容压入栈（根据索引搜索）
>   2. `ldc_w`接收16位参数，索引常量池的范围更大
>   3. `ldc2_w`用于压入long类型和double类型数据
>
> c-3 出栈装入局部变量表
>
> - 将操作数栈中栈顶元素弹出，装入局部变量表中指定的位置
> - 指令以store形式存在，分为`xstore`和`xstore_n`
> - 1. `xstore：x`为i、l、f、d、a，在后面需要跟一个byte的参数指定存带局部变量表中哪个位置
>   2. `xstore_n`：x为i、l、f、d、a，n代表局部变量表中的索引，即将栈顶元素存到局部变量表中哪个位置
>
> **D 算数指令**
>
> - 对两个操作数栈上的值进行运算，并发结果压入操作数栈
> - 大体上分为两类：整数运算和浮点运算
> -  有专门针对byte、short、char、boolean的算数指令，对于这四个类型，使用int类型的指令来处理 

> D-1 算数运算符
>
> - 加法指令：`iadd、ladd、fadd、dadd`
> - 减法指令：`isub、lsub、fsub、dsub`
> - 乘法指令：`imul、lmul、fmul、dmul`
> - 除法指令：`idiv、ldiv、fdiv、ddiv`
> - 求余指令：`irem、lrem、frem、drem`
> - 取反指令：`ineg、lneg、fneg、dneg`
> - 自增指令：`iinc`（在局部变量表内执行）
> - 位运算指令，分为：
>
> 1. 位移指令：`ishl、ishr、iushr、lshl、lshr、lushr`
> 2. 按位或指令：`ior、lor`
> 3. 按位与指令：`iand、land`
> 4. 按位异或指令：`ixor、lxor`
>
>  **E 创建对象实例指令** 
>
> `new` 接收一个操作数，该操作数是指向常量池的索引，表示要创建的对象的类型，执行完毕以后，将对象的引用压入栈 
>
> `init` :对象构造器方法， 在程序执行 `new` 一个对象调用该对象类的 constructor 方法时才会执行`init`方法
>
> `clinit`是类构造器方法, 在`JVM`进行类加载—验证—解析—初始化，中的初始化阶段会调用`clinit`方法。
>
> E-1 对象的创建：（Java中除基本类型外都是引用类型）
>
> 1. `newarray`：创建基本类型数组
> 2. `anewarrat`：创建引用类型数组（数组中的元素是对象/引用类型）
> 3. `multianewarray`：创建多维数组
>
> E-2 字段访问指令:
>
> 1. `getstatic`：将字段压入操作数栈
> 2. `putstatic`：将字段弹出操作数栈（字段赋值）
>
> E-3 方法调用指令：
>
> 1. ` invokevirtual`: 用于调用对象的实例方法，根据对象的实际类型进行分派（虚方法分派），支持多态 
> 2. ` invokeinterface `: 用于调用接口方法 
> 3. ` invokespecial `: 调用特殊处理的实例方法（没有重写的方法），包括初始化方法（构造器）、私有方法和父类方法。都属于静态类型绑定，不会在调用时动态分发
> 4. ` invokestatic `:  调用类方法（static静态方法），也属于静态绑定，只要有static修饰，就使用`invokestatic`指令 
>
> E-4 方法返回指令：
>
> > 方法的返回指令是由方法定义的返回类型决定的，如果返回的值不是定义的类型，那么会自动转为定义的类型 
>
> 1. 有返回：`ireturn`（boolean、byte、char、short、int类型）、`lreturn`、`freturn`、`dreturn`、`areturn`
> 2. 无返回：return （void）
> 3. 通过返回指令，将当前函数操作栈的栈顶元素弹出，压入调用者函数的操作数栈，然后当前函数操作数栈被抛弃
>
> **F 操作数栈管理指令：**
>
> 1. 将一个或两个元素（一个或两个slot）从栈顶弹出，并直接废弃弹出元素：`pop`、`pop2`
> 2. 复制栈顶一个或两个数值，并将复制值或双份的复制值重新压入栈顶：`xdup`、`dup2`、`dup_x1`、`dup2_x1`、`dup_x2`、`dup2_x2`
> 3.  不带_x的指令（`dup`、`dup2`）：复制栈顶元素并压入栈顶，`dup`后面的系数表示要复制的slot个数，`dup`复制一个slot，`dup2`复制两个slot
>    带_x的指令（`dup_x1`、`dup2_x1`、`dup_x2`、`dup2_x2`）：复制栈顶元素，并压入栈中指定位置，将`dup`的系数和x的系数相加，就得到插入的槽位（从被复制的值开始数）
> 4. 交换栈顶两个slot数值：swap（不能交换64位-long和double）
> 5. 特殊指令`nop`，字节码是`0x00`，和汇编中的`nop`一样，表示啥也不用干，一般用于调试和占位
>
> **G 异常处理指令**
>
> 1. Java中显式抛出异常（throw）都是由`athrow`指令实现
>
> 2. 除了显式抛出异常，还有一些是`JVM`运行时自动检测的，比如除数为0的情况，这个时候是不会有专门的异常指令的，在`idiv`或`ldiv`指令时就会抛出异常了
>
> 3. 正常情况下，操作数栈的压入和弹出都是一条一条的完成的，唯一例外的就是在异常抛出时，Java虚拟机会清除操作数栈上的所有内容，然后将异常实例压入调用函数的操作栈中
>
>    





`Kotlin`官链：https://www.kotlincn.net/docs/reference/sealed-classes.html#%E5%AF%86%E5%B0%81%E7%B1%BB

`JVM`指令参考：https://blog.csdn.net/qq_36370187/article/details/113431049

`Dart枚举`官链：https://www.kotlincn.net/docs/reference/sealed-classes.html#%E5%AF%86%E5%B0%81%E7%B1%BB